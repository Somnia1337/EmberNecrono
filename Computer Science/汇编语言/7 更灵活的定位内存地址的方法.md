👈 [[6 包含多个段的程序]]

### and 和 or 指令

and 指令：按位与。

```asmatmel
mov al, 01100011B
and al, 00111011B
```

执行后 AL = `00100011B`。

or 指令：按位或。

```asmatmel
mov al, 01100011B
or al, 00111011B
```

执行后 AL = `01111011B`。

### 关于 ASCII 码

### 以字符形式给出的数据

用 `'...'` 的形式指明数据是以字符形式给出的，编译器将它们转换成对应的 ASCII 码。

```asmatmel
assume cs:code, ds:data

data segment
	db 'unIX'
	db 'foRK'
data ends

code segment
start:  mov al, 'a'
		mov bl, 'b'
		mov ax, 4C00H
		int 21H
code ends
end start
```

`db unIX` 相当于 `db 75H, 6EH, 49H, 58H`，其余同理。

### 大小写转换的问题

补充 `code` 段，将 `data` 段的第一个字符串转换为大写，第二个转换为小写。

```asmatmel
assume cs:code, da:data

data segment
	db 'BaSiC'
	db 'iNfOrMaTiOn'
data ends

code segment
start:  ; todo
code ends
end start
```

只需能够判断一个字母的大小写，然后对大写字母加上 `20H` 即可得到对应小写字母（反之亦然）：

```asmatmel
code segment
start:  mov ax, data
		mov ds, ax
		
		mov bx, 0
		mov cx, 5
s:      mov al, [bx]
		; 如果 (al)>61H, 即为小写字母, 则 sub al, 20H
		mov [bx], al
		inc bx
		loop s
		
		mov ax, 4C00H
		int 21H
code ends
end start
```

> [!fail] 无法实现
> 
> 这段判断大小写的伪代码无法用已学指令实现。

重新观察，发现大小写字母的二进制除第 5 位（大写 `0`，小写 `1`），其余各位都一样。

```asmatmel
code segment
start:  mov ax, data
		mov ds, ax
		mov bx, 0
		
		mov cx, 5
s:      mov al, [bx]
		and al, 11011111B
		mov [bx], al
		inc bx
		loop s
		
		mov bx, 5
		mov cx, 11
s0:     mov al, [bx]
		or al, 00100000B
		mov [bx], al
		inc bx
		loop s0
		
		mov ax, 4C00H
		int 21H
code ends
end start
```

### `[bx + idata]`

`[bx + idata]` 表示一个内存单元，其偏移地址为 `(bx) + idata`，段地址默认为 `(ds)`。

以下指令都是等效的：

```asmatmel
mov ax, [bx+200]
mov ax, [200+bx]
mov ax, 200[bx]
mov ax, [bx].200
```

### 用 `[bx + idata]` 的方式进行数组的处理

重复大小写转换的问题，但是用 `[bx + idata]` 进行简化：

```asmatmel
code segment
start:  mov ax, data
		mov ds, ax
		
		mov bx, 0
		mov cx, 5
s:      mov al, [bx]
		and al, 11011111B
		mov [bx], al
		mov al, [5+bx]
		or al, 00100000b
		mov [5+bx], al
		inc bx
		loop s
		
		mov ax, 4C00H
		int 21H
code ends
end start
```

用 C 描述：

```c
main() {
	for (int i = 0; i < 5; i++) {
		a[i] &= 0xDF;
		b[i] |= 0x20;
	}
}
```

### SI 和 DI

SI 和 DI 是与 BX 功能相近的寄存器，不过它们不能分为两个 $8$ 位寄存器使用。

以下指令都是等效的：

```asmatmel
mov bx, 0
mov ax, [bx]
; 和
mov si, 0
mov ax, [si]
; 还有
mov di, 0
mov ax, [di]
```

### `[bx + si]` 和 `[bx + di]`

`[bx + si]` 表示一个内存单元，其偏移地址为 `(bx) + (si)`，段地址默认为 `(ds)`。

### `[bx + si + idata]` 和 `[bx + di + idata]`

与上述类似。

### 不同的寻址方式的灵活应用

补充 `code` 段，将 `data` 段中每个单词改为大写：

```asmatmel
assume cs:code, ds:data
data segment
	db 'ibm             ' ; 长为 16 Byte
	db 'dec             '
	db 'dos             '
	db 'vax             '
data ends

code segment
start:  ; todo
code ends
end start
```

进行 $4 \times 3$ 的二重循环：

```asmatmel
start:  mov ax, data
		mov ds, ax
		mov bx, 0
		
		mov cx, 4
s0:     mov si, 0
		
		mov cx, 3
s:      mov al, [bx+si]
		and al, 11011111B
		mov [bx+si], al
		inc si
		loop s
		
		add bx, 16
		loop s0
		
		mov ax, 4C00H
		int 21H
```

> [!fail] 错误
> 
> 这段代码无法按预期工作。

问题在于，二重循环使用了同一个计数器 CX，从而内层循环 `s` 将覆写外层循环 `s0` 的 CX，而 loop 默认 CX 为计数器使得不可能再用另一个计数器。

只需在 `s` 开始前将 `s0` 的计数器保存，每次 `s` 循环之后再恢复到 CX。不过保存到另一个寄存器也不合适，而应保存在内存中，栈对于这种嵌套的结构最为合适：

```asmatmel
; ...
data ends

stack segment
	dw 0, 0, 0, 0, 0, 0, 0, 0
stack ends

code segment
start:  mov ax, data
		mov ds, ax
		mov bx, 0
		
		mov ax, stack
		mov ss, ax
		mov sp, 16
		
		mov cx, 4
s0:     push cx ; 保存 s0 的 CX
		mov si, 0
		mov cx, 3
s:      mov al, [bx+si]
		and al, 11011111B
		mov [bx+si], al
		inc si
		loop s
		
		add bx, 16
		pop cx ; 恢复 s0 的 CX
		loop s0
		
		mov ax, 4C00H
		int 21H
; ...
```

👉 [[8 数据处理的两个基本问题]]