👈 [[5 BX 和 loop 指令]]

程序取得所需的空间有两种方法：

- 加载时分配
- 运行时申请（不讨论）

要在加载时分配空间，需定义一个段，产生了多个段的程序。

### 在代码段中使用数据

对以下 8 个数据求和，结果存储在 AX 中：

```text
0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
```

有多个数据，最好用 `loop` 累加，就需要存储在一组地址连续的内存单元中，因此需要向 OS 申请内存。

在程序中定义这组数据，执行程序时它们随之被加载到内存，自然获取了空间：

```asmatmel
assume cs:code

code segment
	dw 0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
	
	mov bx, 0
	mov ax, 0
	
	mov cx, 8
s:  add ax, cs:[bx]
	add bx, 2
	loop s
	
	mov ax, 4C00H
	int 21H
code ends

end
```

代码段的首行 `dw`(define word) 定义了 8 个字型数据，它们的段地址与程序相同、存储在 CS 中，由于是第一句、偏移地址为 0，因此它们的地址为 `cs:0` ~ `cs:E`。

![[Snipaste_240318_141457.png|500]]

![[Snipaste_240318_142118.png|500]]

连接成 `.exe` 后，需要在 Debug 中手动设置 IP = `0010`，以指定程序的入口语句 `mov bx, 0`，如果不这样做，程序执行将不会得到目标结果。

改进程序，使得连接产生的 `.exe` 可以直接执行：

```asmatmel
assume cs:code

code segment
	dw 0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
	
start:  mov bx, 0
		mov ax, 0
		
		mov cx, 8
s:      add ax, cs:[bx]
		add bx, 2
		loop s
		
		mov ax, 4C00H
		int 21H
code ends

end start
```

在第一句前使用标号 `start` 指明程序的入口，并将末行 `end` 改为 `end start`。

含数据程序的框架：

```asmatmel
assume cs:code

code segment
	; 数据

start:
	; 代码

code ends

end start
```

### 在代码段中使用栈

补全程序，用栈将程序数据倒序存放：

```asmatmel
assume cs:code

codesg segment
	dw 0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
	
	; todo

codesg ends

end
```

思路：将数据存放在一系列字单元（`cs:0` ~ `cs:F`）中，依次入栈（栈段为 `cs:10` ~ `cs:2F`），再依次出栈到相同位置。

```asmatmel
assume cs:code

codesg segment
	dw 0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
	dw 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
	; 定义 16 个字型数据, 从而取得相应的空间, 以作为栈段使用
	
start:  mov ax, cs
		mov ss, ax
		mov sp, 30H
		
		mov bx, 0
		mov cx, 8
s:      push cs:[bx]
		add bx, 2
		loop s ; 入栈
		
		mov bx, 0
		mov cx, 8
s0:     pop cs:[bx]
		add bx, 2
		loop s0 ; 出栈
		
		mov ax, 4C00H
		int 21H
codesg ends

end start
```

栈段为 `cs:10` ~ `cs:2F`，初始时栈空，需设置 `ss:sp` 指向栈底，因此设置 SS:SP = `cs:30`。

### 将数据、代码、栈放入不同的段

类似于定义代码段，可以在一个程序中定义多个段：

```asmatmel
assume cs:code, ds:data, ss:stack

data segment
	dw 0123H, 0456H, 0789H, 0ABCH, 0DEFH, 0FEDH, 0CBAH, 0987H
data ends

stack segment
	dw 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
stack ends

code segment
start:  mov ax, stack
		mov ss, ax
		mov sp, 20H
		
		mov ax, data
		mov ds, ax
		mov bx, 0
		
		mov cx, 8
s:      push [bx]
		add bx, 2
		loop s
		
		mov bx, 0
		
		mov cx, 8
s0:     pop [bx]
		add bx, 2
		loop s0
		
		mov ax, 4C00H
		int 21H
code ends
end start
```

段名会被编译器替换为段地址，`mov ax, data` 将段名为 `data` 的段（数据段）的段地址存入 AX。

数据段中的数据 `0ABCH` 的地址为 `data:6`，要将其存入 BX：

```asmatmel
mov ax, data
mov ds, ax
mov bx, ds:[6]
```

不能使用：

```asmatmel
mov ds, data ; error: 不能将值直接存入段寄存器
mov bx, ds:[6]
```

更不能使用：

```asmatmel
mov bx, data:[6] ; error: 这种格式的 `:` 前需为段寄存器
```

👉 [[7 更灵活的定位内存地址的方法]]